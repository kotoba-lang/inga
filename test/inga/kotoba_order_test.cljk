(ns inga.kotoba-order-test
  (:require [clojure.test :refer [deftest is]]
            [inga.attest :as attest]
            [inga.consensus :as consensus]
            [inga.kotoba-order :as order]))

(def witnesses #{"a" "b" "c" "d"})
(def chain-id "kotoba-incidence-live")
(def hash-fn #(str "hash:" (hash %)))
(def sign-fn (fn [w] #(str w "|" %)))
(def verify-sig (fn [w payload sig] (= sig ((sign-fn w) payload))))

(defn fixture []
  (let [block (consensus/make-block
               {:height 1 :parent-hash nil
                :proposals ["bafy-entry-a" "bafy-entry-b"]
                :proposer "dataspace:org/example" :ts 42 :round 0})
        commit-id (hash-fn (consensus/canonical-block block))
        votes (mapv (fn [w]
                      (-> (consensus/make-vote w commit-id 1)
                          (assoc :inga.vote/view 0)
                          (attest/sign-vote chain-id 0 (sign-fn w))))
                    ["a" "b" "c"])
        qc (attest/certify (consensus/qc votes 4 0) votes)]
    {:consensus/profile order/profile
     :consensus/dataspace "dataspace:org/example"
     :consensus/height 1
     :consensus/parent-id nil
     :consensus/commit-id commit-id
     :consensus/entry-cids ["bafy-entry-a" "bafy-entry-b"]
     :consensus/certificate {:inga/order-block block :inga/qc qc}}))

(deftest qc-binds-the-whole-kotoba-order-envelope
  (let [verify! (order/verifier {:chain-id chain-id :quorum 3
                                 :hash-fn hash-fn :verify-sig-fn verify-sig
                                 :admitted? witnesses})
        envelope (fixture)
        expected (assoc (select-keys envelope order/envelope-binding-fields)
                        :consensus/valid? true)]
    (is (= expected (verify! envelope)))
    (doseq [tampered [(assoc envelope :consensus/dataspace "dataspace:evil")
                      (assoc envelope :consensus/height 2)
                      (assoc envelope :consensus/parent-id "fork")
                      (assoc envelope :consensus/entry-cids ["bafy-reordered"])
                      (assoc-in envelope
                                [:consensus/certificate :inga/qc
                                 :inga.qc/sigs "a"] "forged")]]
      (is (nil? (verify! tampered))))))

(deftest unknown-witness-and-below-quorum-fail-closed
  (let [envelope (fixture)]
    (is (nil? ((order/verifier {:chain-id chain-id :quorum 4
                                :hash-fn hash-fn :verify-sig-fn verify-sig
                                :admitted? witnesses}) envelope)))
    (is (nil? ((order/verifier {:chain-id chain-id :quorum 3
                                :hash-fn hash-fn :verify-sig-fn verify-sig
                                :admitted? #{"a" "b"}}) envelope)))))
